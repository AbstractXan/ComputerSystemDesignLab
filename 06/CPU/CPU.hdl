// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/05/CPU.hdl

/**
 * The Hack CPU (Central Processing unit), consisting of an ALU,
 * two registers named A and D, and a program counter named PC.
 * The CPU is designed to fetch and execute instructions written in 
 * the Hack machine language. In particular, functions as follows:
 * Executes the inputted instruction according to the Hack machine 
 * language specification. The D and A in the language specification
 * refer to CPU-resident registers, while M refers to the external
 * memory location addressed by A, i.e. to Memory[A]. The inM input 
 * holds the value of this location. If the current instruction needs 
 * to write a value to M, the value is placed in outM, the address 
 * of the target location is placed in the addressM output, and the 
 * writeM control bit is asserted. (When writeM==0, any value may 
 * appear in outM). The outM and writeM outputs are combinational: 
 * they are affected instantaneously by the execution of the current 
 * instruction. The addressM and pc outputs are clocked: although they 
 * are affected by the execution of the current instruction, they commit 
 * to their new values only in the next time step. If reset==1 then the 
 * CPU jumps to address 0 (i.e. pc is set to 0 in next time step) rather 
 * than to the address resulting from executing the current instruction. 
 */

CHIP CPU {

    IN  inM[16],         // M value input  (M = contents of RAM[A])
        instruction[16], // Instruction for execution
        reset;           // Signals whether to re-start the current
                         // program (reset==1) or continue executing
                         // the current program (reset==0).

    OUT outM[16],        // M value output
        writeM,          // Write to M? 
        addressM[15],    // Address in data memory (of M)
        pc[15];          // address of next instruction

    PARTS:
      // Identify A or C instr
      Or(a=instruction[15], b=false, out=isCinstr); // if(instruction[15]==1) => C type instruction
      Not(in=instruction[15],        out=isAinstr); // if not C => A type instruction

      And(a=isCinstr, b=instruction[5], out=isCWriteA);               // isCinstr and destination is A, write A = true
      Or(a=isAinstr, b=isCWriteA, out=loadA);                         // write A if (A-instr || C write A instruction)

	  // Input to A reg = instruction if CWriteA else, previous value form ALU
      Mux16(a=instruction, b=outALU, sel=isCWriteA, out=inA);

	  // Setup A register chip, outputs, value and address	 
      ARegister(in=inA, load=loadA, out=outA, out[0..14]=addressM);   
  
	  // If value to be stored in D register
      And(a=isCinstr, b=instruction[4], out=loadD);                   
      DRegister(in=outALU, load=loadD, out=outDReg);

	  // Switch between A or M depending on 'a' value of c instruction      
      Mux16(a=outA, b=inM, sel=instruction[12], out=outAorM);            
      

	  // Setup ALU
      ALU(x=outDReg,y=outAorM,zx=instruction[11], nx=instruction[10],zy=instruction[9],ny=instruction[8],
			f=instruction[7],no=instruction[6],out=outALU,out=outM,zr=isZeroOut,ng=isNegOut);
      
	  // +ve , -ve , equal to zero
      Not(in=isNegOut, out=isNonNeg);
      Not(in=isZeroOut, out=isNonZero);
      And(a=isNonNeg, b=isNonZero, out=isPositive); 
      
	  // If value to be stored in M
      And(a=isCinstr, b=instruction[3], out=writeM);
      
      And(a=isPositive, b=instruction[0], out=JGT);
      And(a=isZeroOut, b=instruction[1], out=JEQ);
      And(a=isNegOut, b=instruction[2], out=JLT);
          

      Or(a=JEQ, b=JLT, out=JLE);
      Or(a=JLE, b=JGT, out=jumptoA);            // jump?

      And(a=isCinstr, b=jumptoA, out=loadPC);   // loadPC? if(isCinstr and jump)
      Not(in=loadPC, out=PCinc);
      PC(in=outA, inc=PCinc, load=loadPC, reset=reset, out[0..14]=pc);
}
